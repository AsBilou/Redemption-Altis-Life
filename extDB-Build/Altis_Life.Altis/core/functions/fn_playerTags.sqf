#include <macro.h>
/*
	File: fn_playerTags.sqf
	Author: Bryan "Tonic" Boardwine
	
	Description:
	Adds the tags above other players heads when close and have visible range.
*/
private["_ui","_units","_goggles","_uniform","_headgear"];
#define iconID 78000
#define scale 0.8

_goggles = ["Balaclava_Black","Balaclava_GRY","Balaclava_OD","Shemagh_Face","Shemagh_FaceTan","Shemagh_FaceRed","Shemagh_FaceGry","Shemagh_FaceOD","T_HoodOD","T_HoodTanBlk","T_HoodACU","T_HoodBlk","T_HoodMD","T_HoodMW","T_HoodTan","T_HoodBDU","T_HoodTanCLR","T_HoodODCLR","T_HoodODBLK","T_HoodACUBLK","T_HoodACUCLR","T_HoodBDUCLR","T_HoodBDUBLK","T_HoodBlkBLK","T_HoodBlkCLR","T_HoodMDBLK","T_HoodMDCLR","T_HoodMWCLR","T_HoodMWBLK"];
_uniform = ["U_O_GhillieSuit"];
_headgear = ["H_Shemag_khk","H_Shemag_tan","H_Shemag_olive","H_Shemag_olive_hs","H_RacingHelmet_1_F","H_RacingHelmet_2_F","H_RacingHelmet_3_F","H_RacingHelmet_4_F","H_RacingHelmet_1_black_F","H_RacingHelmet_1_blue_F","H_RacingHelmet_1_green_F","H_RacingHelmet_1_red_F","H_RacingHelmet_1_white_F","H_RacingHelmet_1_yellow_F","H_RacingHelmet_1_orange_F","max_biker_helmet","Kio_Balaclava"];


if(visibleMap OR {!alive player} OR {dialog}) exitWith {
	500 cutText["","PLAIN"];
};

_ui = uiNamespace getVariable ["Life_HUD_nameTags",displayNull];
if(isNull _ui) then {
	500 cutRsc["Life_HUD_nameTags","PLAIN"];
	_ui = uiNamespace getVariable ["Life_HUD_nameTags",displayNull];
};

_units = nearestObjects[(visiblePosition player),["Man","Land_Pallet_MilBoxes_F","Land_Sink_F"],50];

_units = _units - [player];

{
	private["_text"];
	_idc = _ui displayCtrl (iconID + _forEachIndex);
	if(!(lineIntersects [eyePos player, eyePos _x, player, _x]) && {!isNil {_x getVariable "realname"}}) then {
		_pos = switch(typeOf _x) do {
			case "Land_Pallet_MilBoxes_F": {[visiblePosition _x select 0, visiblePosition _x select 1, (getPosATL _x select 2) + 1.5]};
			case "Land_Sink_F": {[visiblePosition _x select 0, visiblePosition _x select 1, (getPosATL _x select 2) + 2]};
			default {[visiblePosition _x select 0, visiblePosition _x select 1, ((_x modelToWorld (_x selectionPosition "head")) select 2)+.5]};
		};
		_sPos = worldToScreen _pos;
		_distance = _pos distance player;
		if(count _sPos > 1 && {_distance < 15}) then {
			_text = switch (true) do {
				case ((goggles _x) in _goggles): {format["<t color='#000000'>Masked Player</t>"];};
				case ((headgear _x) in _headgear): {format["<t color='#000000'>Masked Player</t>"];};
				case ((uniform _x) in _uniform): {format["<t color='#000000'>Masked Player</t>"];};
				case (_x in (units grpPlayer) && playerSide == civilian): {format["<t color='#00FF00'>%1</t>",(_x getVariable ["realname",name _x])];};
				case (!isNil {(_x getVariable "rank")}): {format[" [%1] %2",switch ((_x getVariable "rank")) do {
					case 1: {"Recrue"};
					case 2: {"Agent"}; 
					case 3: {"Caporal"};
					case 4: {"Sergent"};
					case 5: {"Lieutenant"};
					case 6: {"Capitaine"};
					case 7: {"Major"};
					case 8: {"Colonel"};
					case 9: {"Commandant"};
					case 10:{"Chef Police"};
					},_x getVariable ["realname",name _x]]};
				case ((!isNil {_x getVariable "name"} && playerSide == independent)): {format["<t color='#FF0000'><img image='a3\ui_f\data\map\MapControl\hospital_ca.paa' size='1.5'></img></t> %1",_x getVariable ["name","Unknown Player"]]};
				default {
					if(!isNil {(group _x) getVariable "gang_name"}) then {
						format["%1<br/><t size='0.8' color='#B6B6B6'>%2</t>",_x getVariable ["realname",name _x],(group _x) getVariable ["gang_name",""]];
					} else {
						_x getVariable ["realname",name _x];
					};
				};
			};
			
			_idc ctrlSetStructuredText parseText _text;
			_idc ctrlSetPosition [_sPos select 0, _sPos select 1, 0.4, 0.65];
			_idc ctrlSetScale scale;
			_idc ctrlSetFade 0;
			_idc ctrlCommit 0;
			_idc ctrlShow true;
		} else {
			_idc ctrlShow false;
		};
	} else {
		_idc ctrlShow false;
	};
} foreach _units;